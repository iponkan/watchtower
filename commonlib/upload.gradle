apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

// 版本
version = "0.0.2"

// 根节点添加
def siteUrl = 'https://github.com/sonicers/commonlib'    // project homepage
def gitUrl = 'https://github.com/sonicers/commonlib.git' // project git

// 根节点添加
group = "com.sonicers"

// 根节点添加
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                name 'sonicers commonlib'
                url siteUrl
                // Set your license
                licenses {
                    license {
                        name 'MIT License'
                        url 'https://raw.githubusercontent.com/sonicers/commonlib/master/LICENSE'
                    }
                }
                developers {
                    developer { //填写的一些基本信息
                        id 'iponkan'
                        name 'hiccup.you'
                        email 'iponkan@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}
javadoc {
    options {
        encoding "UTF-8"
        charSet 'UTF-8'
    }
}

//配置bintray参数
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintrayUser")
    key = properties.getProperty("bintrayApiKey")
    configurations = ['archives']
    pkg {
        repo = "maven"               //跟上面创建的Maven仓库名字保持一致
        name = "commonlib"                //发布到JCenter上的项目名字
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["MIT"]
        publish = true
    }
}